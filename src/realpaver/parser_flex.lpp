%{
#include <stdio.h>
#include "realpaver/parser_bison.hpp"
#include "realpaver/Problem.hpp"

extern int parser_bison_lineno;
%}

%option prefix = "parser_bison_"
%option nounput

SPACE       [ \t\r]+
IDENT       [a-zA-Z][a-zA-Z0-9_]*
DIG         [0-9]
INT         {DIG}+
E           [eE][+-]?{DIG}+
REAL        ({DIG}+{E}|{DIG}*"."{DIG}+({E})?|{DIG}+"."{DIG}*({E})?)

%%
{INT}       return TK_INT;
{REAL}      return TK_REAL;

"var"       return TK_STR_VAR;
"const"     return TK_STR_CONST;
"real"      return TK_STR_REAL;
"int"       return TK_STR_INT;
"min"       return TK_MIN;
"max"       return TK_MAX;
"sqrt"      return TK_SQRT;
"log"       return TK_LOG;
"exp"       return TK_EXP;
"tan"       return TK_TAN;
"cos"       return TK_COS;
"sin"       return TK_SIN;
"sgn"       return TK_SGN;
"abs"       return TK_ABS;
"in"        return TK_IN;

"+"         return TK_PLUS;
"-"         return TK_MINUS;
"*"         return TK_MUL;
"/"         return TK_DIV;
"^"         return TK_POW;
"("         return TK_LPAR;
")"         return TK_RPAR;
"["         return TK_LSBR;
"]"         return TK_RSBR;
","         return TK_COMMA;
";"         return TK_SCOLON;
":"         return TK_COLON;
">"         return TK_GT;
"<"         return TK_LT;
"="         return TK_EQ;
">="        return TK_GE;
"<="        return TK_LE;

{IDENT}     return TK_IDENT;

"\n"     { ++parser_bison_lineno; }

{SPACE}  {}

"/*"     {
            int c;
            for( ;; )
            {
               while( (c=getchar()) != '*' && c!=EOF )
               {
                  if( c=='\n' ) ++parser_bison_lineno;
               }

               if( c=='*' )
               {
                  while( (c=getchar()) == '*' )
                  {
                     if( c=='\n' ) ++parser_bison_lineno;
                  }

                  if( c=='/' ) break;
                  if( c=='\n' ) ++parser_bison_lineno;
               }
               if( c==EOF ) break;
            }
         }

"//"     {
            int c;
            while( (c=getchar()) != '\n' && (c!=EOF) )
            {}
            if( c=='\n' ) ++parser_bison_lineno;
         }
%%

realpaver::Problem* parser_bison_problem;

YY_BUFFER_STATE parser_bison_buffer;
char parser_error[256];

void parser_flex_init(realpaver::Problem * prob)
{
   parser_bison_lineno = 1;
   parser_bison_problem = prob;
   strcpy(parser_error, "");

}

void parser_flex_init_str(realpaver::Problem * prob, const char *str)
{
   parser_flex_init(prob);
   parser_bison_buffer = parser_bison__scan_string(str);
}

int parser_flex_init_file(realpaver::Problem * prob, const char *filename)
{
   int res = 0;
   parser_bison_in = fopen(filename, "r");
   if (!parser_bison_in)
   {
      strcpy(parser_error, "file not found");
      res = 1;
   }
   else
   {
      parser_flex_init(prob);
      parser_bison_buffer =
         parser_bison__create_buffer(parser_bison_in, YY_BUF_SIZE);
      parser_bison__switch_to_buffer(parser_bison_buffer);
   }
   return res;
}

void parser_flex_cleanup_str()
{
   parser_bison_problem = NULL;
   parser_bison__delete_buffer(parser_bison_buffer);
}

void parser_flex_cleanup_file()
{
   parser_bison_problem = NULL;
   if (parser_bison_in)
   {
      fclose(parser_bison_in);
      parser_bison__delete_buffer(parser_bison_buffer);
   }
}

int parser_bison_wrap(void)
{
   return 1;
}
